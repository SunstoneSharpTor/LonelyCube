/*
  Lonely Cube, a voxel game
  Copyright (C) g 2024-2025 Bertie Cartwright

  Lonely Cube is free software: you can redistribute it and/or modify
  it under the terms of the GNU General Public License as published by
  the Free Software Foundation, either version 3 of the License, or
  (at your option) any later version.

  Lonely Cube is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.

  You should have received a copy of the GNU General Public License
  along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

layout(location = 0) in vec4 position;
layout(location = 1) in vec2 texCoord;
layout(location = 2) in float skyLightLevel;
layout(location = 3) in float blockLightLevel;

out vec2 v_texCoord;
out float v_skyBrightness;
out float v_blockBrightness;
out float v_visibility;

uniform mat4 u_modelView;
uniform mat4 u_proj;
uniform float u_renderDistance;
uniform float u_skyLightIntensity;

const float fogStart = 0.4;
const float fogDensity = 1.85;
const float minDarknessAmbientLight = 0.00002;
const float blockLightIntensity = 0.04;

void main() {
    float maxDarknessAmbientLight = min(0.001f, u_skyLightIntensity);

    vec4 positionRelativeToCam = u_modelView * position;
    gl_Position = u_proj * positionRelativeToCam;
    v_texCoord = texCoord;
    float factor = skyLightLevel * skyLightLevel * skyLightLevel;
    //factor *= factor;
    v_skyBrightness = u_skyLightIntensity / (1.0 + (1.0 - skyLightLevel) * (1.0 - skyLightLevel) * 45.0)
        * factor + maxDarknessAmbientLight / (1.0 + (1.0 - skyLightLevel) * (1.0 - skyLightLevel) *
        45.0) * (1.0 - factor);
    v_skyBrightness = max(v_skyBrightness, minDarknessAmbientLight);
    factor = blockLightLevel * blockLightLevel;
    v_blockBrightness = blockLightIntensity / (1.0 + (1.0 - blockLightLevel) * (1.0 - blockLightLevel) * 45.0)
        * factor + maxDarknessAmbientLight / (1.0 + (1.0 - blockLightLevel) * (1.0 - blockLightLevel) *
        45.0) * (1.0 - factor);

    float distance = length(positionRelativeToCam.xyz);
    float normalisedDistance = clamp(distance - u_renderDistance * fogStart, 0.0f, u_renderDistance
      * (1.0f - fogStart)) / (u_renderDistance * (1.0f - fogStart));
    v_visibility = normalisedDistance * fogDensity;
    v_visibility = exp(-v_visibility * v_visibility);
    v_visibility *= clamp((u_renderDistance - distance) / u_renderDistance, 0.0f, 0.04f) * 25.0f;
};
